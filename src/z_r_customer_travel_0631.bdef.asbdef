managed implementation in class zbp_r_customer_travel_0631 unique;
strict ( 2 );
with draft;

define behavior for Z_R_CUSTOMER_TRAVEL_0631 alias CustomerTravel
persistent table zcustomers_0631
draft table zcustomers_d_631
lock master
total etag LastChangedAt
etag master LocalLastChangedAt
authorization master ( global, instance )

{
  create;
  update;
  delete;
  field ( numbering : managed, readonly ) CustomerUuid;
  field ( mandatory ) CustomerId;
  field ( readonly ) Description, FlightDate, OverallStatus;
  field ( features : instance ) Price;

  action ( features : instance, authorization : update ) acceptClients
    parameter Z_AE_CUSTOMER_TRAVEL_0631 result [1] $self;
  action ( features : instance, authorization : update ) rejectClients result [1] $self;

  internal action updateFlightDate;

  validation validateCustomer on save { create; field CustomerId; }

  determination setDescription on save { create; field Description; }
  determination setStatus on modify { create; field OverallStatus, FlightDate; }

  determine action validateCustomerFormat { validation validateCustomer; }

  side effects
  { field CustomerId affects field Description;
    determine action validateCustomerFormat executed on field CustomerId affects messages; }

  draft action Resume;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;

  draft determine action Prepare { validation validateCustomer; }

  mapping for zcustomers_0631
    {
      CustomerUuid       = customer_uuid;
      CustomerId         = customer_id;
      FlightDate         = flight_date;
      Description        = description;
      Price              = price;
      CurrencyCode       = currency_code;
      OverallStatus      = overall_status;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }

}